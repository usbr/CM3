! SJR_restrict.wresl
! Nancy Parker
! 11/02, USBR
! This code restricts regulated flows in various arcs within the SJR system
! Revised by C. Hsu, 3/3/03, USBR, for review and documentation project
! ###############################################################################
/*
Restrict Millerton, Hensley, and Eastman regulated releases to deliveries and losses
The balance of release will be flood-releted
*/
! Restrict Millerton regulated release to losses and diversions from Friant to Gravelly Ford, and Settlement flows
!goal boundC18main { C18m < SJRR_rel  }
!define SJRR_L605 {value max(0., min(SJRR_rel - C18_min*taf_cfs, 0.05*(SJRR_rel - C18_min*taf_cfs) + 95.))}
!goal setC603_mainSJRR {C603_mainSJRR < max(0.,SJRR_rel - C18_min*taf_cfs - SJRR_L605)}
!Snow releases and ibu releases from Millerton must come from FWUA account.
goal FWUA_rel_MINandSNOW {C18m + C18A < C18_FWUA}
! Restrict Hensley regulated release to Fresno River losses, Fresno River delivery and Fresno River minimum instream flow 
goal boundC52main { C52M < L588 + D588 + C588_MIF }
! Restrict Eastman regulated release to Chowchilla River delivery, Chowchilla River losses and Madera River flow 
goal boundC53main { C53m < D582 + L582 - C590}

/*
The following codes define maximum permissible regulated flows at Goodwin (node 16)
per USBR operations within SANJASM and STANMOD.

MAXPERMISSIBLE
Node   MaxLimit
16     1500.001
528    1500.001
*/
! Regulated releases from Goodwin Dam (Tulloch Reservoir) < user defined capacity (i.e. 1500 cfs)
define maxGoodwin { select MaxLimit from MaxPermissible where node=16 }
! Set limits on Goodwin release
goal RestrGoodwin{ C520 - C520F < maxGoodwin }

/*
Water released from New Melones for WQ, VAMP, and DO must flow past Goodwin (if below channel capacity)
*/
! Water released from New Melones for water quality route past Goodwin
goal routeC520WQ {C520WQ = C10WQ}
! Water released from New Melones for VAMP route past Goodwin
goal routeC520VAMP {C520VAMP = C10VAMP_M}
! Water released from New Melones for DO route past Goodwin
goal routeC520DO {C520DO = C10DO}
! Water released from New Melones for minimum flow (?) route past Goodwin
goal routeC520MIN {C520MIN = C10MIN}
! Water released from New Melones for Stan NMFS RPA routed past Goodwin
goal routeC520RPA {C520RPA = C10RPA}
! Water released from New Melones for instream flow route past Goodwin
!goal routeC520INSTREAM {C520INSTREAM = C10INSTREAM}

/*
Chowchilla Bypass operations
The Chowchilla Bypass splits Millerton flood flows that flow past Gravelly Ford
(Millerton does not release water for demands past Gravelly Ford).
The Bypass structure allows 2000 cfs to flow into the Mendota Pool before routing water
through the Chowchilla Bypass (in order to protect the Riparian land around the Mendota
Pool.  The Bypass has a channel capacity of 6500 cfs which if exceeded will flood
back into the Mendota Pool.  Therefore, the operations are as follows:
   1)up to 2000 cfs through C605A_main
   2)the next 6500 cfs through C605B
   3)any flows above 8500 cfs through C605A_flood
System defined restrictions on C605B give it an upper limit of 6500 and a negative weight
C605A_flood needs to have a larger negative weight than C605B for a correct order of filling
*/
! integer variable to control chowchilla bypass use
! don't allow diversions if C605A < mendota pool capacity
! C605A is already split into C605A_main and C605A_flood, which is highly penalized
define int_C605A {INTEGER STD KIND 'INTEGER' UNITS 'NONE'}  ! 0 if C605A < 1400; 1 if C605A > 1400
goal boundC605A_0 {C605A_main > int_C605A * 1400.}
goal boundC605A_1 {C605A_flood < int_C605A * 9999999.}
goal boundC605b {C605B < int_C605A * 6500.}

!Route Settlement flow to Sack Dam.   
goal setC605C {C605C = C603_mainsjrr}

!Route Settlement flow beyond Sack Dam
!L608 is intended to represent losses in restored channel, not channel connecting Mendota Pool with Sack Dam.
goal setC608 {C608 > C605C - L608}  

